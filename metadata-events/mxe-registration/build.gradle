apply plugin: 'java'

configurations {
  avroOriginal
}

dependencies {
  compile project(':metadata-events:mxe-avro-1.7')
  compile project(':metadata-models')
  compile spec.product.pegasus.dataAvro1_6

  testCompile project(':test-models')

  avroOriginal project(path: ':metadata-models', configuration: 'avroSchema')
  
  constraints {
    implementation("org.apache.logging.log4j:log4j-core:2.17.0") {
        because("previous versions are vulnerable to CVE-2021-45105")
    }
    implementation("org.apache.logging.log4j:log4j-api:2.17.0") {
        because("previous versions are vulnerable to CVE-2021-45105")
    }
  }
}

// copy original MXE avro schema from metadata-models to resources
task copyOriginalMXESchemas(type: Copy) {
  dependsOn configurations.avroOriginal

  from { // use of closure defers evaluation until execution time
    configurations.avroOriginal.collect { zipTree(it) }
  }
  into("src/main/resources/")
  include("avro/com/linkedin/mxe/")
}

compileJava.dependsOn copyOriginalMXESchemas
processResources.dependsOn copyOriginalMXESchemas

clean {
  project.delete("src/main/resources/avro")
}